{
  "cells": [
    {
      "cell_type": "markdown",
      "source": [
        "# Sellke construction of the SIR model\nSean L. Wu (@slwu89), 2021-11-30\n\n## Introduction\n\nThe Sellke construction is an alternative stochastic process to the standard stochastic epidemic\nprocess that can be used to sample the final size distribution of an SIR epidemic in a finite population,\noriginally described in [On the asymptotic distribution of the size of a stochastic epidemic](https://doi.org/10.2307/3213811).\n\nRather than sampling next events and times under a race condition to update the system as in\nstandard stochastic epidemics, the Sellke construction samples at once a \"threshold\" value\nfor each susceptible individual. The \"infection pressure\" generated by the infectious population\nis tracked, and when it exceeds the threshold for a given susceptible person, they become infected.\n\nThis construction is similar to a [random time change](http://bactra.org/notebooks/random-time-changes.html) such\nthat the thresholds are independent and identically distributed exponential random variates with a mean of 1,\nand infection pressure is the cumulative hazard $\\beta \\, I/N$.\n\n## Libraries"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using Distributions\nusing Random\nusing BenchmarkTools\nusing Plots"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Sellke model\n\nCode to draw a final epidemic size from a standard continuous-time Markov SIR model\nis below. The Sellke construction can handle non-exponentially distributed infectious\nperiods, which simply require the the infectious durations to be sampled from the appropriate\ndistribution. Another nice feature of the Sellke epidemic model is that parameters can\neasily be changed without needing to draw new random numbers.\n\nBecause the construction samples the final epidemic size, and then creates a corresponding\ntemporal trajectory, we do not specify the time domain.\n\nThis particular implementation is described in the paper [How big is an outbreak likely to be? Methods for epidemic final-size calculation](https://royalsocietypublishing.org/doi/full/10.1098/rspa.2012.0436)."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "function sellke(u0, p)\n    (S, I, R) = u0\n    N = S + I + R\n    (β, c, γ) = p\n    λ = β*c/N\n\n    Q = rand(Exponential(), S)\n    sort!(Q)\n\n    T0 = rand(Exponential(1/γ), I)\n    T = rand(Exponential(1/γ), S)\n\n    ST0 = sum(T0)\n    Y = [ST0; ST0 .+ cumsum(T[1:end-1])]\n\n    Z = findfirst(Q .> Y*λ)\n\n    if isnothing(Z)\n        Z = S + I # entire population infected\n    else\n        Z = Z + I - 1\n    end\n\n    TT = [T0; T] # all infectious periods\n    QQ = [T0 * 0; Q] # all thresholds \n    R = T[1:I] # recovery times of the initial infectives\n\n    # max num of events possible\n    max = I + 2*S\n\n    t = zeros(max)\n    St = zeros(max)\n    It = zeros(max)\n    It[1:I] = (1:I)\n    St[1:I] = N .- (1:I)\n\n    tt = 0\n    La = 0\n    j = I+1\n    k = j\n\n    while It[k-1] > 0\n        (minR, i) = findmin(R)\n        dtprop = minR-tt\n        Laprop = La + (λ * It[k-1] * dtprop)\n        if j > length(QQ) # only recoveries remain\n            R = R[setdiff(1:length(R), i)]\n            tt = minR\n            t[k] = minR\n            It[k] = It[k-1]-1\n            St[k] = St[k-1]\n            La = Laprop\n            k = k+1\n        else # infections\n            if QQ[j] > Laprop\n                R = R[setdiff(1:length(R), i)]\n                tt = minR\n                t[k] = minR\n                It[k] = It[k-1]-1\n                St[k] = St[k-1]\n                La = Laprop\n            else\n                tt = tt + ((QQ[j]-La)/(Laprop-La))*dtprop\n                La = QQ[j]\n                t[k] = tt\n                It[k] = It[k-1]+1\n                St[k] = St[k-1]-1\n                R = [R; tt+TT[j]]\n                j = j+1\n            end\n            k = k+1\n        end\n    end\n    \n    trajectory = hcat(t, St, It, N .- (St + It))\n    trajectory = trajectory[I:k-1, :]\n\n    return trajectory\nend"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Initial conditions"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "u0 = [990,10,0]; # S,I,R"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Parameter values"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "p = [0.05,10.0,0.25]; # β,c,γ"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Random number seed\n\nWe set a random number seed for reproducibility."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "Random.seed!(1234);"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Running the model"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "out = sellke(u0, p)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Plotting\n\nWe plot a histogram of final epidemic sizes."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "plot(out[:, 1], out[:, 2:end],\n    label=[\"S\" \"I\" \"R\"],\n    xlabel=\"Time\",\n    ylabel=\"Number\")"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Benchmarking"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "@benchmark sellke(u0, p)"
      ],
      "metadata": {},
      "execution_count": null
    }
  ],
  "nbformat_minor": 2,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia",
      "version": "1.6.2"
    },
    "kernelspec": {
      "name": "julia-1.6",
      "display_name": "Julia 1.6.2",
      "language": "julia"
    }
  },
  "nbformat": 4
}
